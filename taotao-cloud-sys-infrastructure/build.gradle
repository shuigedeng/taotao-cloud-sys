/**
 * DDD: infrastructure 基础实施层  最底层(但与所有层进行交互)
 * 向其他层提供 通用的 技术能力(比如工具类,第三方库类支持,常用基本配置,数据访问底层实现)
 * 基础实施层主要包含以下的内容:
 * 为应用层 传递消息(比如通知)
 * 为领域层 提供持久化机制(最底层的实现)
 * 为用户界面层 提供组件配置
 * 基础设施层还能够通过架构框架来支持四个层次间的交互模式。
 *
 * 2.基础层承载数据访问和entity 工具层承载工具代码 不依赖本项目其它模块 只依赖一些通用工具包
 * 同时承载基础服务（ES、Redis、MQ）
 *
 * 这一层是一个适配层，主要负责外部系统和内部业务系统的适配，
 * 这一层的主要作用就是外部系统和内部系统的适配和协议转换。
 */


plugins {
    id("nu.studer.jooq") version "10.2"
}

dependencies {
    api project(":taotao-cloud-sys-application")
    api project(":taotao-cloud-sys-domain")

    annotationProcessor "com.querydsl:querydsl-apt:5.1.0:jakarta"
    api "com.querydsl:querydsl-jpa:5.1.0:jakarta"
    api "jakarta.persistence:jakarta.persistence-api:3.2.0"
    annotationProcessor "jakarta.persistence:jakarta.persistence-api:3.2.0"

    api 'com.mysql:mysql-connector-j:9.5.0'

    testImplementation("org.hibernate.tool:hibernate-tools-orm:7.2.1.Final")
    // jOOQ代码生成依赖（注意版本匹配）
    jooqGenerator 'com.mysql:mysql-connector-j:9.5.0'
    jooqGenerator 'org.jooq:jooq-meta-extensions:3.20.10'
}



// jOOQ配置
jooq {
    version = '3.20.10'  // 与dependencies中的jooq一致

    configurations {
        main {  // 主配置
            generateSchemaSourceOnCompilation = false

            //https://github.com/jOOQ/jOOQ/blob/version-3.20.5/jOOQ-meta/src/main/java/org/jooq/meta/jaxb/Configuration.java
            generationTool {
                //logging = org.jooq.meta.jaxb.Logging.WARN

                // JDBC连接配置
                jdbc {
                    driver = 'com.mysql.cj.jdbc.Driver'
                    url = 'jdbc:mysql://127.0.0.1:3306/taotao-cloud-sys?useSSL=false&serverTimezone=Asia/Shanghai&nullCatalogMeansCurrent=true&useInformationSchema=true&nullNamePatternMatchesAll=true&allowPublicKeyRetrieval=true'
                    user = 'root'
                    password = '123456'
                }

                // 生成器配置
                generator {
                    name = 'org.jooq.codegen.JavaGenerator'

                    // 数据库配置
                    database {
                        name = 'org.jooq.meta.mysql.MySQLDatabase'
                        // 或 PostgreSQL
                        // name = 'org.jooq.meta.postgres.PostgresDatabase'

                        // 包含的表（正则表达式）
                        //includes = '*'
                        // 排除的表
//                        excludes = '''
//                            flyway_schema_history |
//                            databasechangelog |
//                            databasechangeloglock
//                        '''

                        // 输入schema
                        inputSchema = 'taotao-cloud-sys'

                        // 自定义类型映射
//                        forcedTypes {
//                            forcedType {
//                                name = 'vARCHAR'
//                                includeExpression = '.*\\.(name|title|description)'
//                                includeTypes = 'VARCHAR\\(255\\)'
//                            }
//                            forcedType {
//                                name = 'BOOLEAN'
//                                includeTypes = '(?i:TINYINT\\(1\\))'
//                            }
//                        }
                    }

                    // 生成目标
                    //https://github.com/jOOQ/jOOQ/blob/version-3.20.10/jOOQ-meta/src/main/java/org/jooq/meta/jaxb/Generate.java
                    generate {
                        // 生成的内容
                        pojos = true              // 生成POJO
                        daos = true               // 生成DAO
                        springDao = true               // 生成DAO
                        immutablePojos = false    // 不可变POJO
                        fluentSetters = true      // 流畅的setter
                        javaTimeTypes = true      // 使用Java8时间类型

                        // ✅ 注解的正确配置（是独立的属性，不是嵌套对象）：
                        jpaAnnotations = true        // 生成 @Entity, @Table, @Id 等
                        validationAnnotations = true  // 生成 @NotNull, @Size 等
                        springAnnotations = true     // 生成 @Repository 等

                        // ✅ Java 8+ 时间类型
                        javaTimeTypes = true

                        // 关联关系
                        relations = true
                        deprecated = false
                        records = true
                        globalObjectReferences = true
                        globalCatalogReferences = false
                        globalSchemaReferences = false
                        globalTableReferences = false
                        globalSequenceReferences = false
                        globalUDTReferences = false
                        globalRoutineReferences = false
                        globalQueueReferences = false
                        globalLinkReferences = false
                        generatedAnnotationJooqVersion=false
                        jooqVersionReference=false

                        generatedAnnotation = true
                        // 配置注释位置
                        // 这些配置影响注释生成
                        comments = true           // 启用注释
                        commentsOnCatalogs = true
                        commentsOnSchemas = true
                        commentsOnTables = true
                        commentsOnColumns = true  // 字段注释

                        // 关键：关闭方法上的注释
                        javadoc = false  // 不生成方法上的JavaDoc
                    }

                    // 目标配置
                    target {
                        packageName = 'com.taotao.cloud.sys.infrastructure.jooq'
                        directory = 'build/generated/sources/jooq/main'
                        encoding = 'UTF-8'
                    }

                    // 策略配置
                    strategy {
                        name = 'org.jooq.codegen.DefaultGeneratorStrategy'
                        // 或自定义策略
                        // name = 'com.example.CustomGeneratorStrategy'
                    }
                }
            }
        }

    }
}


// 定义一个独立的配置，避免污染主依赖
configurations {
    mybatisGen
}

dependencies {
    mybatisGen 'org.mybatis.generator:mybatis-generator-core:1.4.2'
    mybatisGen 'org.mybatis.dynamic-sql:mybatis-dynamic-sql:1.5.2'
    mybatisGen 'io.github.shuigedeng:taotao-boot-starter-data-mybatis:2026.02'
    mybatisGen 'com.mysql:mysql-connector-j:9.5.0'
    //mybatisGenerator 'org.postgresql:postgresql:42.7.8'
    //mybatisGenerator  // Here add your mariadb dependencies or else
}

// 自定义 Gradle 任务
task generateMybatis(type: JavaExec) {
    classpath = configurations.mybatisGen
    mainClass = 'org.mybatis.generator.api.ShellRunner'
    args '-configfile', 'generatorConfiguration.xml'
    args '-overwrite'
}

